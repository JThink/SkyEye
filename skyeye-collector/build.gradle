apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'maven'
apply plugin: 'org.springframework.boot'
apply plugin: 'application'

group = 'skyeye'
applicationName = 'skyeye-collector'
version = '1.0.0'

sourceCompatibility = 1.8
targetCompatibility = 1.8
compileJava.options.encoding = 'UTF-8'
buildDir = 'target'

repositories {
  mavenLocal()
  maven { url "https://repository.cloudera.com/artifactory/cloudera-repos" }
  maven { url "http://192.168.88.8:8081/nexus/content/groups/public" }
  maven { url "http://192.168.88.8:8081/nexus/content/repositories/releases" }
  mavenCentral()
}

ext {
  kafkaVersion = '0.10.0.1'
  esVersion = '2.3.3'
  traceVersion = '1.0.0'
  dataVersion = '1.0.0'
  zkclientVersion = '0.9.1-up'
  hadoopVersion = '2.6.0-cdh5.4.0'
  fastJsonVersion = '1.2.28'
}

dependencies {
  compile "skyeye:skyeye-data-jpa:$dataVersion"
  compile "skyeye:skyeye-data-rabbitmq:$dataVersion"
  compile "skyeye:skyeye-data-hbase:$dataVersion"
  compile ("org.springframework.boot:spring-boot-starter") {
    exclude group: 'ch.qos.logback', module: 'logback-classic'
    exclude group: 'ch.qos.logback', module: 'logback-core'
  }

  compile "org.apache.kafka:kafka-clients:$kafkaVersion"

  compile "org.elasticsearch:elasticsearch:$esVersion"

  compile "org.springframework.boot:spring-boot-starter-data-redis"

  compile "com.101tec:zkclient:$zkclientVersion"

  compile "commons-lang:commons-lang:2.6"

  compile ("org.apache.hadoop:hadoop-common:$hadoopVersion") {
    exclude group: 'tomcat', module: 'jasper-runtime'
    exclude group: 'tomcat', module: 'jasper-compiler'
  }

  compile ("org.apache.hadoop:hadoop-hdfs:$hadoopVersion") {
    exclude group: 'tomcat', module: 'jasper-runtime'
    exclude group: 'tomcat', module: 'jasper-compiler'
  }

  compile ("org.apache.commons:commons-dbcp2:2.1.1")
  compile ("mysql:mysql-connector-java:5.1.39")

  compile "com.alibaba:fastjson:$fastJsonVersion"

  testCompile "org.springframework.boot:spring-boot-starter-test"
}

configurations {
  compile.exclude group: "log4j", module: "log4j"
  compile.exclude group: "org.slf4j", module: "slf4j-log4j12"
  compile.exclude group: "org.springframework", module: "spring-web"
}

mainClassName = 'com.jthink.skyeye.collector.launcher.Launcher'

buildscript {
  ext {
    springBootVersion = '1.5.6.RELEASE'
  }

  repositories {
    mavenLocal()
    maven { url "http://192.168.88.8:8081/nexus/content/groups/public" }
    mavenCentral()
  }

  dependencies {
    classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
    classpath("io.spring.gradle:dependency-management-plugin:1.0.3.RELEASE")
  }
}

startScripts {
  doLast {
    unixScript.text = unixScript.text.replaceAll("lib/(.*)\n", "lib/\\*")
  }
}